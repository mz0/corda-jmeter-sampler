buildscript {
    def confFile = 'corda-sampler.config'
    Properties conf = new Properties()
    if (file(confFile).exists()) {
        file(confFile).withInputStream { conf.load(it) }
        ext.jmeterHome = conf.getProperty("JMETER_HOME")
        ext.artifactory_user = conf.getProperty("CORDA_ARTIFACTORY_USERNAME")
        ext.artifactory_password = conf.getProperty("CORDA_ARTIFACTORY_PASSWORD")
    } else {
        ext.jmeterHome = System.getenv("JMETER_HOME") ?: "/opt/jmeter5"
        ext.artifactory_user = System.getenv("CORDA_ARTIFACTORY_USERNAME")
        ext.artifactory_password = System.getenv("CORDA_ARTIFACTORY_PASSWORD")
    }
}

plugins {
    id("org.jetbrains.kotlin.jvm") version '1.2.71'
    id 'java-library'
}

allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            languageVersion = "1.2"
            apiVersion = "1.2"
            jvmTarget = "1.8"
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = "https://software.r3.com/artifactory/corda-dependencies" }
    maven {
        url = "https://software.r3.com/artifactory/r3-corda-releases"
        credentials {
            username = artifactory_user
            password = artifactory_password
        }
    }
}
 
configurations {
    all*.exclude group: 'org.apache.jmeter', module: 'jorphan'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.71"
    api "com.r3.corda:corda-core:4.3"
    implementation "com.r3.corda:corda-rpc:4.3"
    api 'com.r3.corda:corda-ptflows:4.3.1'
    compileOnly 'org.apache.jmeter:ApacheJMeter_java:5.2'
}

jar {
    baseName project.name
}

task copyLib(type:Copy) {
    from configurations.default
    into "$jmeterHome/lib"
    duplicatesStrategy DuplicatesStrategy.WARN
    exclude 'kotlin-stdlib-jdk7*', 'slf4j-api-1.7.25*', 'jcl-over-slf4j-1.7.2*', 'commons-jexl3-3.0*', 'asm-5.0.4*'
    exclude 'jackson-databind-2.9.5*', 'jackson-core-2.9.5*', 'caffeine-2.6.2*' // corda-4.1
    exclude 'jackson-databind-2.9.7*', 'jackson-core-2.9.7*', 'caffeine-2.7.0*' // corda-4.[23]
    exclude 'jackson-annotations-2.9.0*', 'error_prone_annotations-2.1.3*', 'liquibase-core-*'
    exclude 'hibernate-commons-annotations*', 'groovy-all*', 'hibernate-core*' // ?Saxon-HE, ?xalan,
}

task install(type:Copy, dependsOn:[build, copyLib]) {
    from jar
    into "$jmeterHome/lib/ext"
}

wrapper {
    gradleVersion = "5.4.1"
    distributionType = Wrapper.DistributionType.ALL
}
